plugins {
    id 'java'
    id 'com.adarshr.test-logger' version '4.0.0'
    id 'io.freefair.lombok' version '8.6'
    id 'io.spring.dependency-management' version '1.1.+'
    id "org.jetbrains.kotlin.jvm" version "1.9.+"
    id 'org.springframework.boot' version '3.2.+'
}


java { sourceCompatibility = '21' }
repositories { mavenCentral() }
bootRun { jvmArgs(["-Xms2g", "-Xmx2g"]) }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'io.github.hakky54:logcaptor:2.9.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

if (System.env.APPROACH == "loom-tomcat") {
    dependencies {
        // Takes precedence over spring-boot-starter-webflux if both on classpath
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
    println 'Added spring-boot-starter-web dependency for approach loom-tomcat'
}

test {
    useJUnitPlatform()
    jvmArgs '-XX:+EnableDynamicAgentLoading'
    jvmArgs '-Xshare:off'
}
